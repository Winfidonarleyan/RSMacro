import oralib, likepy, rsd, rslscr, globals;

private macro GetClientCode() : Integer
  record Panel(ACC, "test.lbr") dialog;

  private macro SMsgProc(ob, cmd, id, key)
    if ((cmd == DLG_KEY) AND (key == 13))
      return CM_SELECT;
    end;

    return CM_DEFAULT;
  end;

  private macro AddCol(ar, ind, fld, head, width)
    ar.value(ind * 6 + 0) = fld;
    ar.value(ind * 6 + 1) = head;
    ar.value(ind * 6 + 2) = width;
    ar.value(ind * 6 + 3) = 2; // fldType
    ar.value(ind * 6 + 4) = -1; // decPoint
    ar.value(ind * 6 + 5) = 0; // reserv
  end;

  macro MsgProc(dlg, cmd, id, key)
    if ((CMD == DLG_KEY) AND (key == 317))
      var ColNames : TArray = TArray;
      var rsCmd = RsdCommand("select t_client, t_nameaccount from daccount_dbt");
      var ob = RsdRecordset(rsCmd, RSDVAL_CLIENT, RSDVAL_STATIC);

      AddCol(ColNames, 0, "t_client", "Номер клиента", null);
      AddCol(ColNames, 1, "t_nameaccount", "Наименование клиента", null);

      if (ob.moveNext() AND RunScroll(ob, 2, ColNames, "ChooseClientID", @SMsgProc, "Выбор клиента", "", false, -1, -1, 70, 30))
        dlg.ClientID = ob.value("t_client");
      else
        dlg.ClientID = 0;
      end;
    end;
  end;

  var clientCode = 0;

  if (RunDialog(Panel, @MsgProc))
    clientCode = Panel.ClientID;
  end;

  return clientCode;
end;

class ClientInfo(id: Integer)
  private var name, fullName;
  private var clientID = id;
  private var isExistClinet = false;

  private macro GetRSDParty()
    var query = RsdRecordset(String("select t_shortname, t_name from dparty_dbt where t_partyid = ", clientID));

     if (query.moveNext())
      name = query.value("t_shortname");
      fullName = query.value("t_name");
      isExistClinet = true;
    end;
  end;

  macro PrintNames()
    PrintLn("Полное наименование: ", fullName);
    PrintLn("Краткое наименование: ", name);
  end;

  macro PrintAdress()
    var queryAdress = RsdRecordset(String("SELECT t.T_ADRESS as addr, tp.T_NAME as type FROM DADRESS_DBT t, DADRTYPE_DBT tp WHERE T_PARTYID = ", clientID, " and TP.T_TYPE = T.T_TYPE"));

    while (queryAdress.moveNext())
      PrintLn(queryAdress.value("type"), " адрес: ", queryAdress.value("addr"), ".");
    end;
  end;

  macro PrintContact()
    var strVariables = "t1.T_NAME as name, t2.T_NAME as type, t.T_VALUE as value";
    var strFrom = "DCONTACT_DBT t LEFT JOIN DCONTACTKIND_DBT t1 ON t1.T_KIND = t.T_CONTACTKIND LEFT JOIN DCONTACTTYPE_DBT t2 ON t.T_CONTACTTYPE = t2.T_TYPE";

    var queryContact = RsdRecordset(String("SELECT ", strVariables, " FROM ", strFrom, " WHERE t.T_PARTYID = ", clientID));

    while (queryContact.moveNext())
      var adrName = queryContact.value("name");
      var adrType = queryContact.value("type");
      var adrValue = queryContact.value("value");

      var fullAddress : String = adrName;

      Print(adrName);

      if (adrType)
        Print(" (", adrType, ")");
      end;

      PrintLn(": ", adrValue);
    end;
  end;

  macro PrintCodeList()
    var queryCode = RsdRecordset(String("SELECT des.T_NAME as name, code.T_CODE as code FROM dobjkcode_dbt des, dobjcode_dbt code WHERE code.T_OBJECTID = ", clientID, " and code.T_CODEKIND = des.T_CODEKIND AND des.T_OBJECTTYPE = 3;"));

    while (queryCode.moveNext())
      PrintLn(queryCode.value("name"), ": ", queryCode.value("code"));
    end;
  end;

  macro PrintInfo()
    if (isExistClinet == false)
      PrintLn("Нет информации по клиенту с кодом: ", clientID);
      PrintLn("Такого клиента не существует, либо вы указали неверный код");
      return;
    end;

    PrintLn("Информация по клиенту с кодом: ", clientID);

    PrintNames();
    PrintCodeList();
    PrintAdress();
    PrintContact();
  end;

  /* After init, check if exist*/
  GetRSDParty();
end;

class ClientInfoAccount()
  var ID, AccountID, Chapter, AccountStatus, Money, MoneyLimit, CurrencyName, CurrencyShortName, CurrencyExternalCode: Integer;

  private macro UpdateCurrencyByCode(code: Integer)
    var queryCurrency = RsdRecordset(String("select t_name_currency, t_short_name, t_externalcode from dcurrency_dbt where t_code_currency = ", code));

    if (queryCurrency.moveNext())
      CurrencyName = queryCurrency.value("t_name_currency");
      CurrencyShortName = queryCurrency.value("t_short_name");
      CurrencyExternalCode = queryCurrency.value("t_externalcode");
    end;
  end;

  private macro UpdateMoney(code: Integer)
    var accountFreeRest = $0;
    var accountFreeRestLimit = $0;

    if (AccGetFreeAmount(accountFreeRest, accountFreeRestLimit, ID, Chapter, code, {curdate}))
      PrintLn("Ошибка при определении свободного остатка для счёта '", ID, "'");
    end;

    Money = accountFreeRest;
    MoneyLimit = accountFreeRestLimit;
  end;

  macro UpdateInfo(updateQuery)
    ID = updateQuery.value("t_Account");
    AccountStatus = updateQuery.value("status");
    Chapter = updateQuery.value("t_chapter");
    AccountID = updateQuery.value("t_accountid");

    UpdateCurrencyByCode(updateQuery.value("t_code_currency"));
    UpdateMoney(updateQuery.value("t_code_currency"));
  end;

  macro PrintLineTable(isNeedPrintHeader)
    if (isNeedPrintHeader)
      var headTable = "┌────────────────────────┬────────────────────────┬────────────┬───────────────────────────────┐\n" +
                      "│                        │      Валюта счета      │            │        Текущий остаток        │\n" +
                      "│      Номер счёта       ├───────┬────────────────┤   Статус   ├───────────────┬───────────────┤\n" +
                      "│                        │  Код  │  Наименование  │            │   С лимитом   │  Без лимита   │";

      PrintLn("\nСчета клиента:");
      PrintLn(headTable);
    end;

    PrintLn("├────────────────────────┼───────┼────────────────┼────────────┼───────────────┼───────────────┤");
    [│  ####################  │  #### │  ############  │   #######  │ ###########   │ ###########   │]
    (ID, CurrencyShortName, CurrencyName, AccountStatus, MoneyLimit, Money);
  end;
end;

private macro PrintBaseClientInfo(clientID)
  var info = ClientInfo(clientID);

  info.PrintInfo();
end;

private macro PrintAccountClientInfo(clientID)
  /*                                              0                1                                                                          2          3            4 */
  var query = RsdRecordset(String("select t_Account, t_code_currency, CASE when t_open_close = chr(0) THEN 'Открыт' else 'Закрыт' end as status, t_chapter, t_accountid from daccount_dbt where t_Client = ", clientID));

  if (query.moveNext())
    var accCount = 1;
    var accInfo = ClientInfoAccount();

    accInfo.UpdateInfo(query);
    accInfo.PrintLineTable(true);

    while (query.moveNext())
      accInfo.UpdateInfo(query);
      accInfo.PrintLineTable();
      accCount = accCount + 1;
    end;

    PrintLn("└────────────────────────┴───────┴────────────────┴────────────┴───────────────┴───────────────┘");
    PrintLn("Количество счетов: ", accCount);
  end;
end;

private macro PrintAllClientInfo()
  var clientID = GetClientCode();

  PrintBaseClientInfo(clientID);
  PrintAccountClientInfo(clientID);
end;

/* Init */
PrintAllClientInfo();
